Option Explicit

Private Const INSERT_HEADER_NAME = "time[sec]"
Private Const OFFSET_ROW_NM = 10
Private m_active_ws As Worksheet
Private m_hear_arr_var As Variant
Private m_last_column_lng As Long
Private m_last_row_lng As Long
Private m_row_offset_lng As Long
'***********************************************************
' 機能   : コンストラクタ
' 引数   : なし
' 戻り値 : なし
'***********************************************************
Private Sub Class_Initialize()
    m_last_column_lng = 1
    m_last_row_lng = 1
    m_row_offset_lng = OFFSET_ROW_NM
End Sub
'***********************************************************
' 機能   : コンストラクタ(引数あり)
' 引数   : 列を識別する文字列 String
' 戻り値 : なし
'***********************************************************
Public Function funcInit(ByVal sheet_name_str As String)
    Set m_active_ws = ThisWorkbook.Worksheets(sheet_name_str)
    m_last_column_lng = cmn_getLastColumnNm(1, m_active_ws)
    m_last_row_lng = cmn_getLastRowNm(1, m_active_ws)
    m_hear_arr_var = funcGetHeaderITem
End Function
'***********************************************************
' 機能   : ワークシートオブジェクト（追加したワークシート）を取得する
' 引数   : なし
' 戻り値 : ワークシートオブジェクト Worksheet
'***********************************************************
Public Function funcGetThisWorkSheet() As Worksheet
    Set funcGetThisWorkSheet = m_active_ws
End Function
'***********************************************************
' 機能   :
' 引数   :
' 戻り値 :
'***********************************************************
Public Function funcModifyDataSheet()
    Call funcModifyNumberFormat
    Call funcModifyInsertColumns
    Call funcModifyProperty
    Call funcModifyInsertRows
End Function
'***********************************************************
' 機能   :
' 引数   :
' 戻り値 :
'***********************************************************
Public Function funcModifyDataSheetForAppend(ByRef check_status_var As Variant)
    Call funcModifyNumberFormat
    Call funcExport(check_status_var)
    Call funcModifyInsertColumns
    Call funcModifyProperty
    Call funcModifyInsertRows
    Call funcModifyReferenceCell(check_status_var)
End Function
'***********************************************************
' 機能   : ヘッダーアイテムを取得する
' 引数   : なし
' 戻り値 : ヘッダーアイテムの配列 Variant
'***********************************************************
Private Function funcGetHeaderITem() As Variant
    With m_active_ws
        funcGetHeaderITem = .Range(.Cells(1, 1), .Cells(1, m_last_column_lng))
    End With
End Function
'***********************************************************
' 機能   : セルの表示形式を設定する
' 引数   : なし
' 戻り値 : なし
'***********************************************************
Private Function funcModifyNumberFormat()
    '// フォーマットする列を抽出
    Dim format_nm_lng As Long
    Dim i As Long: i = 0
    For i = 1 To UBound(m_hear_arr_var, 2)
        If m_hear_arr_var(1, i) = "assign1_therm" Then
            format_nm_lng = i
            Exit For
        End If
    Next i

    '// 表示形式を設定する
    With m_active_ws
        .Range(.Cells(2, format_nm_lng), .Cells(m_last_row_lng, format_nm_lng)).NumberFormatLocal = "yyyy/m/d"
    End With
End Function
'***********************************************************
' 機能   : 追加するアイテムをシートに出力
' 引数   : 追加するアイテム String
' 戻り値 : なし
'***********************************************************
Private Function funcExport(ByRef check_status_var As Variant)
    Dim i As Long
    For i = 0 To UBound(check_status_var)
        m_active_ws.Cells(1, (m_last_column_lng + 1) + i).Value = check_status_var(i)
    Next i
End Function
'***********************************************************
' 機能   : セルのプロパティを設定する
' 引数   : なし
' 戻り値 : なし
'***********************************************************
Private Function funcModifyProperty()
    With m_active_ws
        '//表全体に格子状の罫線を引く
        .Range("A1").CurrentRegion.Borders.LineStyle = xlContinuous
        '//セル幅を調整する
        .Range(.Cells(1, 1), .Cells(1, m_last_column_lng)).EntireColumn.AutoFit
    End With
End Function
'***********************************************************
' 機能   : 行の挿入
' 引数   : なし
' 戻り値 : なし
'***********************************************************
Private Function funcModifyInsertRows()
    m_active_ws.rows(1 & ":" & m_row_offset_lng).Insert
    '// メンバー変数に挿入数を追加
    m_last_row_lng = m_last_row_lng + m_row_offset_lng
End Function
'***********************************************************
' 機能   : 列の挿入(B列)
' 引数   : なし
' 戻り値 : なし
'***********************************************************
Private Function funcModifyInsertColumns()
    Dim arr_var() As Variant, val_var As Variant
    Dim i As Long: i = 0

    m_active_ws.Columns(2).Insert '// B列に挿入

    ReDim arr_var(m_last_row_lng - 1)

    For Each val_var In arr_var
        If i = 0 Then
            m_active_ws.Cells(i + 1, 2) = INSERT_HEADER_NAME
        Else
            m_active_ws.Cells(i + 1, 2) = i - 1
        End If
        i = i + 1
    Next val_var

    '// メンバ変数に列数を追加
    m_last_column_lng = m_last_column_lng + 1
End Function
'***********************************************************
' 機能   : セルの参照を追加
' 引数   : なし
' 戻り値 : なし
'***********************************************************
Private Function funcModifyReferenceCell(ByRef check_status_var As Variant)
    Dim i As Long
    For i = 0 To UBound(check_status_var)
        If check_status_var(i) = "表中央" Then
            Debug.Print "i = " & i & " check_status_var(i) = " & check_status_var(i)
            m_active_ws.Cells(2 + m_row_offset_lng, (m_last_column_lng + 1) + i).Formula = "=" & m_active_ws.Cells(4, 4).Address
            m_active_ws.Cells(m_active_ws.Cells(rows.count, 1).End(xlUp).Row, (m_last_column_lng + 1) + i).Formula = "=" & m_active_ws.Cells(4, 5).Address
        End If
        If check_status_var(i) = "表左上" Then
            Debug.Print "i = " & i & " check_status_var(i) = " & check_status_var(i)
            m_active_ws.Cells(2 + m_row_offset_lng, (m_last_column_lng + 1) + i).Formula = "=" & m_active_ws.Cells(5, 4).Address
            m_active_ws.Cells(m_active_ws.Cells(rows.count, 1).End(xlUp).Row, (m_last_column_lng + 1) + i).Formula = "=" & m_active_ws.Cells(5, 5).Address
        End If
    Next i
End Function

